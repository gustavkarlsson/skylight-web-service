plugins {
	id "java"
	id "application"
	id "com.github.johnrengelman.shadow" version "1.2.3"
}

sourceCompatibility = 1.8
group = "se.gustavkarlsson.aurora-notifier"
version = "1.0-SNAPSHOT"
mainClassName = "se.gustavkarlsson.aurora_notifier.web_service.AuroraApplication"

wrapper() {
	gradleVersion = "2.7"
}

repositories {
	mavenCentral()
	maven { url "https://jitpack.io" }
}

dependencies {
	ext {
		dropwizard_version = "1.0.2"
	}

	compile "io.dropwizard:dropwizard-core:$dropwizard_version"
	compile "io.dropwizard:dropwizard-metrics:$dropwizard_version"
	compile "org.jsoup:jsoup:1.9.2"
	compile "ru.vyarus:dropwizard-guicey:4.0.0"
	compile "net.anthavio:airbrake-logback:1.0.3"
	compile "com.github.gustavkarlsson:aurora-notifier-common:aurora-notifier-common-1.5.0"

	testCompile "junit:junit:4.12"
	testCompile "nl.jqno.equalsverifier:equalsverifier:2.1.6"
	testCompile "io.dropwizard:dropwizard-testing:$dropwizard_version"
	testCompile "io.dropwizard:dropwizard-client:$dropwizard_version"
	testCompile "com.squareup.retrofit2:converter-jackson:2.1.0"
}

jar {
	manifest {
		attributes "Implementation-Title": name,
				"Implementation-Version": version,
				"Built-By": System.getProperty("user.name"),
				"Built-Date": new Date(),
				"Built-JDK": System.getProperty("java.version"),
				"Main-Class": mainClassName
	}
}

shadowJar {
	mergeServiceFiles {
		exclude "META-INF/*.SF"
		exclude "META-INF/*.DSA"
		exclude "META-INF/*.RSA"
	}
}

task stage(dependsOn: ["clean", "shadowJar"])

defaultTasks "stage"
